function prompt-length() {
	emulate -L zsh
	local -i x y=${#1} m
	if (( y )); then
		while (( ${${(%):-$1%$y(l.1.0)}[-1]} )); do
			x=y
			(( y *= 2 ))
		done
		while (( y > x + 1 )); do
			(( m = x + (y - x) / 2 ))
			(( ${${(%):-$1%$m(l.x.y)}[-1]} = m ))
		done
	fi
	echo $x
}

function fill-line() {
	local left_len=$(prompt-length $1)
	local right_len=$(prompt-length $2)
	local pad_len=$((COLUMNS - left_len - right_len - 1))
	local pad=${(pl.$pad_len.. .)}  # pad_len spaces
	echo ${1}${pad}${2}
}

function set-prompt-corners() {
	# Arguments (all are as prompt escape strings)
	# 1. Start host color
	# 3. Start Background/muted color
	# Put a basic prompt with username, hostname (first component) and the
	# current directory name.
	# Put the current timestamp at the right side.
	autoload -z get-active-envs end-prompt-colors
	host_color_start=$1
	host_color_end=$(end-prompt-colors $1)
	mute=$2
	local top_left="$(get-active-envs $mute)""[%n@${host_color_start}%m${host_color_end}:%1~${vcs_info_msg_0_}]"
	local top_right='%*'
	local bottom_left='%(?..%F{red})%#'"%(?..$reset_color) "
	# Skipping bottom_right; I don't use it
	PS1="$(fill-line "$top_left" "$top_right")"$'\n'$bottom_left
}
